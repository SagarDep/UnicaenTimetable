apply plugin: 'com.android.application'

configurations {
    compile.exclude group: 'commons-logging' // Referenced in some portable lib. use android internal instead.
}

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'

    packagingOptions {
        exclude('org/apache/commons/codec/language/bm/*.txt') // 124 files (uncompressed 215kb) with unused language specific rules.
    }
    defaultConfig {
        applicationId 'fr.skyost.timetable'
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 46
        versionName 'v2.0.9'
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ['room.schemaLocation': "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue 'string', 'account_type_authority', '"fr.skyost.timetable"'
        }
        debug {
            applicationIdSuffix = '.dev'
            versionNameSuffix '-dev'
            resValue 'string', 'account_type_authority', '"fr.skyost.timetable.dev"'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    def supportVersion = '27.1.1'
    def lifecycleVersion = '1.1.1'
    def roomVersion = '1.1.1'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:$supportVersion"
    implementation "com.android.support:design:$supportVersion"
    implementation "com.android.support:support-v4:$supportVersion"

    implementation "android.arch.lifecycle:extensions:$lifecycleVersion"
    implementation "android.arch.lifecycle:common-java8:$lifecycleVersion"

    implementation "android.arch.persistence.room:runtime:$roomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$roomVersion"

    implementation 'net.sf.biweekly:biweekly:0.6.2'
    implementation 'com.github.apl-devs:appintro:v4.2.3'
    implementation 'io.github.kobakei:ratethisapp:1.2.0'
    implementation 'joda-time:joda-time:2.10'
    implementation 'com.github.quivr:android-week-view:2.3.0'
    implementation 'com.github.QuadFlask:colorpicker:0.0.13'
    implementation 'com.squareup.okhttp3:okhttp:3.11.0'
    implementation 'com.github.matecode:Snacky:1.0.3'
}
